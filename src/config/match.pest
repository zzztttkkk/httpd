WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }

ident   = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
string  = ${ "\"" ~ inner ~ "\"" }
inner   = @{ char* }
char    =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}
strgins =  { "[" ~ (string) ~ ("," ~ string)* ~ "]" }

Logics = { ^"and" | ^"or" }

ConditionKinds     = { ^"method" | ^"path" | ^"query" | ^"header" | ^"cookie" | ^"useragent" | ^"ua" | ^"form" | ^"json" | ^"ref" }
MultiValuesOpts    = { ^"all" | ^"any" | ^"first" | ^"last" | (^"nth(" ~ (ASCII_DIGIT)+ ~ ")") }
ConditionLeftParam = { "<" ~ ident ~ (":" ~ MultiValuesOpts)? ~ ">" }
ConditionLeft      = { ConditionKinds ~ (ConditionLeftParam)? }

QoutedStringOpKinds = { ^"eq" | "==" | ^"gt" | ">" | ^"lt" | "<" | ^"ge" | ">=" | ^"le" | "<=" | ^"contains" | ^"match" }
QoutedStringOp      = { QoutedStringOpKinds ~ string }

IntOpKinds = { ^"igt" | ^"ilt" | ^"ige" | ^"ile" }
IntOp      = { IntOpKinds ~ (ASCII_DIGIT)+ }

InOp = { ^"in" ~ strgins }

Op = { QoutedStringOp | IntOp | InOp }

Not = { ^"not" }

Condition = { ConditionLeft ~ (Not)? ~ (Op)? }

Match = {
    (ident ~ ":")? ~ Logics ~ "{" ~ (Condition ~ ";")+ ~ "}"
}

File = {
    SOI ~ (Match)+ ~ EOI
}
