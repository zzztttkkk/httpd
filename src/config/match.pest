WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }

ident   = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
string  = ${ "\"" ~ inner ~ "\"" }
inner   = @{ char* }
char    =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}
strgins =  { "[" ~ (string) ~ ("," ~ string)* ~ "]" }
int     = @{ (ASCII_DIGIT)+ }

Logics = { ^"and" | ^"or" }

ConditionKinds     = { ^"code" | ^"method" | ^"path" | ^"query" | ^"header" | ^"cookie" | ^"useragent" | ^"ua" | ^"form" | ^"json" | ^"ref" }
MultiValuesOpts    = { ^"all" | ^"any" | ^"first" | ^"last" | (^"nth(" ~ int ~ ")") }
ConditionLeftParam = { "<" ~ ident ~ (":" ~ MultiValuesOpts)? ~ ">" }
ConditionLeft      = { ConditionKinds ~ (ConditionLeftParam)? }

QoutedStringOpKinds = { ^"eq" | ^"gt" | ^"lt" | ^"ge" | ^"le" | ^"contains" | ^"match" }
QoutedStringOp      = { QoutedStringOpKinds ~ string }

IntOpKinds = { "==" | ">" | "<" | ">=" | "<=" }
IntOp      = { IntOpKinds ~ int }

InOp = { ^"in" ~ strgins }

Op = { QoutedStringOp | IntOp | InOp }

Options = { ^"not" | ^"trim" | ^"ignorecase" }

Condition = { ConditionLeft ~ (Options)* ~ (Op)? }

Match = {
    (ident ~ ":")? ~ Logics ~ "{" ~ (Condition ~ ";")+ ~ "}"
}

File = {
    SOI ~ (Match)+ ~ EOI
}
